; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "IChing-Oracle"
#define MyAppVersion "1.0"
#define MyAppPublisher "ichingoracle"
#define MyAppURL "https://ichingoracle.app"
#define MyAppExeName "iching.jar"
#define JREInstall "jre-8u74-windows-i586-iftw.exe"
#define Icon "application.ico"
#define License "license.txt"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{4357C0B1-B757-47E3-8048-9ED8BEB5E2B5}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile={#License}
;InfoBeforeFile=
;InfoAfterFile=
OutputBaseFilename={#MyAppName}-{#MyAppVersion}-setup
OutputDir=Windows
SetupIconFile={#Icon}
UninstallDisplayIcon={uninstallexe}
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Dirs]
Name: "{app}\texts"
Name: "{app}\plugins"

[Files]
; Source: "{#License}"; DestDir: "{app}"; Flags: ignoreversion
Source: "iching-1.0.jar"; DestDir: "{app}"; DestName: "{#MyAppExeName}"; Flags: ignoreversion
Source: "{#JREInstall}"; DestDir: "{tmp}"; DestName: "JREInstall.exe"; Flags: ignoreversion
Source: "plugins\iching-plumblossom-mysteries-1.0.jar"; DestDir: "{app}\plugins"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{uninstallexe}"; WorkingDir: "{app}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{uninstallexe}"; WorkingDir: "{app}"; Tasks: desktopicon

[Run]
Filename: "{tmp}\JREInstall.exe"; Parameters: "/s"; StatusMsg: "Installing Java Runtime Environment..."; BeforeInstall: SetMarqueeProgress(True); Check: not IsJREInstalled
Filename: "{app}\{#MyAppExeName}"; Description: "Launch {#MyAppName}"; Flags: nowait postinstall skipifsilent shellexec; Check: IsJREInstalled

[Code]
#define MinJRE "1.8"

function IsJREInstalled: Boolean;
var
  JREVersion: string;
begin
  Result := RegQueryStringValue(HKLM32, 'Software\JavaSoft\Java Runtime Environment', 'CurrentVersion', JREVersion);
  if not Result and IsWin64 then
    Result := RegQueryStringValue(HKLM64, 'Software\JavaSoft\Java Runtime Environment', 'CurrentVersion', JREVersion);
  if Result then
    Result := CompareStr(JREVersion, '{#MinJRE}') >= 0;
end;

procedure SetMarqueeProgress(Marquee: Boolean);
begin
  if Marquee then
  begin
    WizardForm.ProgressGauge.Style := npbstMarquee;
  end
    else
  begin
    WizardForm.ProgressGauge.Style := npbstNormal;
  end;
end;

